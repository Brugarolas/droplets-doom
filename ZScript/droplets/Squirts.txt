class BloodSquirt : Droplets replaces Blood
{
	Default
	{
		+NOBLOCKMAP
		+DONTTHRUST
		+THRUACTORS
		+NOTELEPORT
		+ALLOWPARTICLES
		+PUFFGETSOWNER
		+NOGRAVITY
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Spray");
		TNT1 A 0 A_Jump(256,"Mid");
		TNT1 A 0
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_Warp(AAPTR_DEFAULT,cos(angle)*8,sin(angle)*8,sin(pitch)*8,0,WARPF_ABSOLUTEOFFSET);
			A_SpawnItemEx("BloodHitPuffSmall",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION);
		}
		stop;
	Spray:
		TNT1 A 0
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_Warp(AAPTR_DEFAULT,cos(angle)*8,sin(angle)*8,sin(pitch)*8,0,WARPF_ABSOLUTEOFFSET);
			A_SpawnItemEx("BloodHitPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION,128-GetCVar("blood_amt")*64);
		}
		stop;
	Mid:
		TNT1 A 0
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_Warp(AAPTR_DEFAULT,cos(angle)*8,sin(angle)*8,sin(pitch)*8,0,WARPF_ABSOLUTEOFFSET);
			A_SpawnItemEx("BloodHitPuffMid",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION,64-GetCVar("blood_amt")*32);
		}
		stop;
	}
}

class BloodHitPuff : Droplets
{
	double user_scale, user_force, user_angmom;
	
	action void A_SpawnDroplet(double drop_size)
	{
		double angle = invoker.angle + frandom(-5.0,5.0);
		double force = invoker.user_force * frandom(0.9,1.1);
		drop_size *= frandom(0.9,1.1);
		let droplet = BloodDroplet(Actor.Spawn('BloodDroplet',invoker.pos + (0,0,4)));
		droplet.final_scale = drop_size;
		droplet.scale = (drop_size, drop_size) * 0.25;
		droplet.Vel3DFromAngle(force, angle, invoker.pitch);
		droplet.translation = invoker.translation;
	}
	
	Default
	{
		+NOBLOCKMAP
		+DONTTHRUST
		+THRUACTORS
		+NOTELEPORT
		+FORCEXYBILLBOARD
		+NOGRAVITY
		Scale 0.75;
		Height 1;
		Radius 1;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			angle = frandom(0.0,360.0);
			user_scale = 0.1615;
			user_force = frandom(8.0,16.0);
			user_angmom = frandom(-15.0,15.0);
			A_SetPitch(frandom(-45.0,90.0));
			A_PlaySound("blood/hit",0,0.3);
			scale.x *= randompick(1,-1);
			A_FloorSplat(0.75);
		}
		BLPF ABCDE 2
		{
			A_SpawnDroplet(user_scale * 1.5);
			angle += user_angmom;
			if (GetCVar("blood_amt") > 0)
			{
				A_SpawnDroplet(user_scale * 2.0);
				A_SpawnDroplet(user_scale * 1.6);
				angle += user_angmom;
				if (GetCVar("blood_amt") > 1)
				{
					A_SpawnDroplet(user_scale * 1.2);
					A_SpawnDroplet(user_scale);
					A_SpawnDroplet(user_scale * 0.8);
					angle += user_angmom;
				}
			}
			user_force *= 0.8;
			user_scale *= 1.2;
		}
		TNT1 A 0;
		Stop;
	}
}

class BloodHitPuffMid : BloodHitPuff
{
	Default
	{
		Scale 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			angle = frandom(0.0,360.0);
			user_scale = 0.1615;
			user_force = frandom(7.0,14.0);
			user_angmom = frandom(-10.0,10.0);
			A_SetPitch(frandom(-45.0,90.0));
			A_PlaySound("blood/hit",0,0.2);
			scale.x *= randompick(1,-1);
		}
		BLPF ABCDE 2
		{
			A_SpawnDroplet(user_scale * 2.0);
			angle += user_angmom;
			if (GetCVar("blood_amt") > 0)
			{
				A_SpawnDroplet(user_scale * 1.2);
				A_SpawnDroplet(user_scale * 0.8);
				angle += user_angmom;
			}
			user_force *= 0.8;
			user_scale *= 1.2;
		}
		TNT1 A 0;
		Stop;
	}
}

class BloodHitPuffSmall : BloodHitPuff
{
	Default
	{
		Scale 0.25;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			angle = frandom(0.0,360.0);
			user_scale = 0.1615;
			user_force = frandom(6.0,12.0);
			user_angmom = frandom(-5.0,5.0);
			A_SetPitch(frandom(-45.0,90.0));
			A_PlaySound("blood/hit",0,0.1);
			scale.x *= randompick(1,-1);
		}
		BLPF ABCDE 2
		{
			A_SpawnDroplet(user_scale);
			angle += user_angmom;
			user_force *= 0.8;
			user_scale *= 1.2;
		}
		TNT1 A 0;
		Stop;
	}
}

class BloodSplatPuff : BloodHitPuff replaces BloodSplatter
{}

class AxeBloodPuff : BloodHitPuff replaces AxeBlood
{}