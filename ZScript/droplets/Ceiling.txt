class CeilingDroplet : DecoDroplet
{
	Default
	{
		Height 1;
		-MISSILE
		+NOGRAVITY
	}
	States
	{
	Spawn:
		TNT1 A 1 NoDelay { alpha = GetCVar("blood_alpha"); }
		BLUD C 6
		{
			SetOrigin((pos.x, pos.y, ceilingz - 1.0), false);
			scale = (0.4,0.05) * final_scale;
		}
		BLUD C 5
		{
			SetOrigin((pos.x, pos.y, ceilingz - 1.0), false);
			scale = (0.37,0.1) * final_scale;
		}
		BLUD C 4
		{
			SetOrigin((pos.x, pos.y, ceilingz - 1.0), false);
			scale = (0.33,0.2) * final_scale;
		}
		BLUD C 3
		{
			SetOrigin((pos.x, pos.y, ceilingz - 1.0), false);
			scale = (0.3,0.3) * final_scale;
		}
		BLUD C 2
		{
			SetOrigin((pos.x, pos.y, ceilingz - 1.0), false);
			scale = (0.28,0.4) * final_scale;
		}
		TNT1 A 0 { bNOGRAVITY = false; scale = (0.25, 0.5) * final_scale; gravity = GetCVar("blood_grav"); }
		BLUD ABCD 2;
		TNT1 A 0 { bMISSILE = true; }
		goto Fall;
	Fall:
		TNT1 A 0 A_JumpIf(pos.z <= floorz, "Death");
		BLUD ABCD 2;
		TNT1 A 0 A_JumpIf(waterlevel > 0, "Underwater");
		Loop;
	}
}

class CeilDripper : Droplets
{
	double final_scale;

	override void Tick()
	{
		Super.Tick();
		SetOrigin((pos.x, pos.y, ceilingz - rndzoffset), false);
	}
	
	action void A_Drip()
	{
		double factor = invoker.final_scale;
		if (factor > 0.5 && frandom(0,100) < factor * 25)
		{
			let droplet = CeilingDroplet(Actor.Spawn('CeilingDroplet',invoker.pos + (frandom(-2,2),frandom(-2,2),0)));
			droplet.final_scale = factor;
			droplet.translation = invoker.translation;
			invoker.final_scale -= frandom(0.00, 0.04);
		}
	}
	
	Default
	{
		Radius 1;
		Height 1;
		+THRUACTORS
		+NOTONAUTOMAP
		+SPAWNCEILING
		+NOGRAVITY
		//DistanceCheck GetCVar(cl_drop1_dist);
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_QueueCorpse;
		PLUD "#" 0 { frame = random(0,2); }
		"####" "#" 0 A_SetAngle(frandom(0.0,360.0));
		"####" "#" 1 A_SetScale(final_scale * 1.00 * GetCVar("blood_size"),0.1);
		"####" "#" 1 A_SetScale(final_scale * 3.13 * GetCVar("blood_size"),0.1);
		"####" "#" 1 A_SetScale(final_scale * 4.2 * GetCVar("blood_size"),0.1);
		"####" "#" 1 A_SetScale(final_scale * 4.73 * GetCVar("blood_size"),0.1);
		"####" "#" 1 A_SetScale(final_scale * 5.00 * GetCVar("blood_size"),0.1);
		goto Drippy;
	Drippy:
		"####" "#" 7 A_Drip;
		goto Drippy;
	}
}