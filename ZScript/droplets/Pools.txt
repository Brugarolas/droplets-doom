class BloodPool2 : Droplets
{
	double user_size, final_scale;
	
	Default
	{
		Radius 1;
		Height 1;
		+NOTONAUTOMAP
		+NOTELEPORT
		+THRUACTORS
		+MOVEWITHSECTOR
		+DONTSPLASH
		RenderStyle "Translucent";
	}
	States
	{
	Spawn:
		TNT1 A 70;
		TNT1 A 0
		{
			if (!master)
				Destroy();
			else if (master.vel.Length() > 0.001)
				SetStateLabel("Spawn");
		}
		TNT1 A 0
		{
			SetOrigin(master.pos, false);
			final_scale = (master.radius)/20.0;
		}
		TNT1 A 0 A_JumpIf(LiquidCheck(),"Fog");
		TNT1 A 0 { user_size = 0.01; }
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0));
		goto Pool;
	Pool:
		PLUD C 1 A_SetScale(user_size, 0.5);
		TNT1 A 0
		{
			user_size = user_size + ((final_scale + 0.05) - user_size) / 20.0;
		}
		TNT1 A 0 A_JumpIf(user_size < final_scale, "Pool");
		goto Done;
	Done:
		PLUD C -1;
		Stop;
	Fog:
		TNT1 A 0 A_BloodFog('BloodFog',final_scale);
		Stop;
	}
}