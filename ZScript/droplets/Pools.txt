class BloodPool2 : Droplets
{
	double user_size;
	
	Default
	{
		Radius 1;
		Height 1;
		+NOTONAUTOMAP
		+NOTELEPORT
		+THRUACTORS
		+MOVEWITHSECTOR
		+DONTSPLASH
		RenderStyle "Translucent";
	}
	States
	{
	Spawn:
		TNT1 A 18;
		TNT1 A 0
		{
			if (!master)
				Destroy();
			else if (master.vel.Length() > 0.001)
				SetStateLabel("Spawn");
		}
		TNT1 A 0 { SetOrigin(master.pos, false); }
		TNT1 A 0 A_JumpIf(LiquidCheck(),"Fog");
		TNT1 A 0
		{
			user_size = 0.1;
		}
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0));
		goto Pool;
	Pool:
		PLUD C 1 A_SetScale(user_size, 0.5);
		TNT1 A 0
		{
			user_size = user_size + (1.05 - user_size) / 175.0;
		}
		TNT1 A 0 A_JumpIf(user_size < 1.0, "Pool");
		goto Done;
	Done:
		PLUD C -1;
		Stop;
	Fog:
		TNT1 A 0 A_BloodFog('BloodFog',1.0);
		Stop;
	}
}