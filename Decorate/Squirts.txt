ACTOR BloodSquirt replaces Blood
{
	+NOBLOCKMAP
	+DONTTHRUST
	+THRUACTORS
	+NOTELEPORT
	+ALLOWPARTICLES
	+PUFFGETSOWNER
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Spray")
		TNT1 A 0 A_Jump(256,"Mid")
		TNT1 A 0
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_Warp(AAPTR_DEFAULT,cos(angle)*8,sin(angle)*8,sin(pitch)*8,0,WARPF_ABSOLUTEOFFSET);
			A_SpawnItemEx("BloodHitPuffSmall",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION);
		}
		stop
	Spray:
		TNT1 A 0
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_Warp(AAPTR_DEFAULT,cos(angle)*8,sin(angle)*8,sin(pitch)*8,0,WARPF_ABSOLUTEOFFSET);
			A_SpawnItemEx("BloodHitPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION,128-GetCVar(blood_amt)*64);
		}
		stop
	Mid:
		TNT1 A 0
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_Warp(AAPTR_DEFAULT,cos(angle)*8,sin(angle)*8,sin(pitch)*8,0,WARPF_ABSOLUTEOFFSET);
			A_SpawnItemEx("BloodHitPuffMid",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION,64-GetCVar(blood_amt)*32);
		}
		stop
	}
}

ACTOR BloodHitPuff
{
	+NOBLOCKMAP
	+DONTTHRUST
	+THRUACTORS
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+NOGRAVITY
	Scale 0.75
	Height 1
	Radius 1
	var float user_force;
	var float user_angmom;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			user_force = frandom(6.0,12.0);
			user_angmom = frandom(-4.5,4.5);
			A_SetPitch(frandom(-45.0,90.0));
			A_PlaySound("blood/hit",0,0.3);
			scalex *= randompick(1,-1);
		}
		BLPF AABBCCDDEE 1
		{
			A_SpawnItemEx("BloodDrop1",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(224-GetCVar(blood_amt)*48));
			angle += user_angmom;
			A_SpawnItemEx("BloodDrop2",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(160-GetCVar(blood_amt)*48));
			angle += user_angmom;
			A_SpawnItemEx("BloodDrop3",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(96-GetCVar(blood_amt)*48));
			angle += user_angmom;
			A_SpawnItemEx("ExcessDroplet",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(256-GetCVar(blood_amt)*64));
			angle += user_angmom;
			user_force *= 0.8;
		}
		TNT1 A 0
		Stop
	}
}

ACTOR BloodHitPuffMid : BloodHitPuff
{
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			user_force = frandom(6.0,12.0);
			user_angmom = frandom(-6.0,6.0);
			A_SetPitch(frandom(-45.0,90.0));
			A_PlaySound("blood/hit",0,0.2);
			scalex *= randompick(1,-1);
		}
		BLPF AABBCCDDEE 1
		{
			A_SpawnItemEx("BloodDrop2",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(160-GetCVar(blood_amt)*48));
			angle += user_angmom;
			A_SpawnItemEx("BloodDrop3",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(96-GetCVar(blood_amt)*48));
			angle += user_angmom;
			A_SpawnItemEx("ExcessDroplet",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(256-GetCVar(blood_amt)*64));
			angle += user_angmom;
			user_force *= 0.8;
		}
		TNT1 A 0
		Stop
	}
}

ACTOR BloodHitPuffSmall : BloodHitPuff
{
	Scale 0.25
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			user_force = frandom(6.0,12.0);
			user_angmom = frandom(-9.0,9.0);
			A_SetPitch(frandom(-45.0,90.0));
			A_PlaySound("blood/hit",0,0.1);
			scalex *= randompick(1,-1);
		}
		BLPF AABBCCDDEE 1
		{
			A_SpawnItemEx("BloodDrop3",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(96-GetCVar(blood_amt)*48));
			angle += user_angmom;
			A_SpawnItemEx("ExcessDroplet",0,0,4,(cos(angle)*user_force),(sin(angle)*user_force),(sin(pitch)*user_force),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY,(256-GetCVar(blood_amt)*64));
			angle += user_angmom;
			user_force *= 0.8;
		}
		TNT1 A 0
		Stop
	}
}

ACTOR BloodSplatPuff : BloodHitPuff replaces BloodSplatter
{}

ACTOR AxeBloodPuff : BloodHitPuff replaces AxeBlood
{}